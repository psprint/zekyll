#!/usr/bin/env zsh

ZERO="${(%):-%N}"
REPO_DIR="${ZERO%/*}"
# On asciinema 0 and ZERO are functionargzero
[ "$ZERO" = "$REPO_DIR" ] && REPO_DIR="$HOME/.zekyll/zekyll"
source "$REPO_DIR/lib/script_preamble"
source "$REPO_DIR/lib/script_vars"
source "$REPO_DIR/lib/script_functions"
source "$REPO_DIR/lib/math_functions"
source "$REPO_DIR/lib/codes_huffman"
source "$REPO_DIR/lib/coding_functions"

#
# Zparseopts
#

ZKL_PERFORM_WRITE="0"
ZKL_HELP="0"
ZKL_PATH=""
ZKL_DEFAULT="0"
ZKL_INDEX="1"
ZKL_SIZE=""
ZKL_VERBOSE="-s"
ZKL_QUIET="0"
ZKL_NOANSI="0"
ZKL_DESCRIPTION="[[]]"
ZKL_SECTION=""
ZKL_CONSISTENT=""
ZKL_LIST=""
ZKL_REPO=""
ZKL_REQ=""
zparseopts -E -D -A opthash h -help q -quiet p: -path: d -default i: -index: \
            s: -size: v -verbose w -write n -noansi -section: -desc: -consistent \
            l -list r: -repo: -req || exit 1

(( ${+opthash[-w]} + ${+opthash[--write]} )) && ZKL_PERFORM_WRITE="1"
(( ${+opthash[-h]} + ${+opthash[--help]} )) && ZKL_HELP="1"
(( ${+opthash[-p]} )) && ZKL_PATH="${opthash[-p]}"
(( ${+opthash[--path]} )) && ZKL_PATH="${opthash[--path]}"
(( ${+opthash[-i]} )) && { ZKL_INDEX="${opthash[-i]}"; [ -z "$ZKL_INDEX" ] && ZKL_INDEX="0"; }
(( ${+opthash[--index]} )) && { ZKL_INDEX="${opthash[--index]}"; [ -z "$ZKL_INDEX" ] && ZKL_INDEX="0"; }
(( ${+opthash[-s]} )) && { ZKL_SIZE="${opthash[-s]}"; [ -z "$ZKL_SIZE" ] && ZKL_SIZE="0"; }
(( ${+opthash[--size]} )) && { ZKL_SIZE="${opthash[--size]}"; [ -z "$ZKL_SIZE" ] && ZKL_SIZE="0"; }
(( ${+opthash[-d]} + ${+opthash[--default]} )) && ZKL_DEFAULT="1"
(( ${+opthash[-v]} + ${+opthash[--verbose]} )) && ZKL_VERBOSE=""
(( ${+opthash[-q]} + ${+opthash[--quiet]} )) && ZKL_QUIET="1"
(( ${+opthash[-n]} + ${+opthash[--noansi]} )) && ZKL_NOANSI="1"
(( ${+opthash[--desc]} )) && ZKL_DESCRIPTION="${opthash[--desc]}"
(( ${+opthash[--section]} )) && ZKL_SECTION="${opthash[--section]}"
(( ${+opthash[--consistent]} )) && ZKL_CONSISTENT="1"
(( ${+opthash[-l]} + ${+opthash[--list]} )) && ZKL_LIST="1"
(( ${+opthash[-r]} )) && ZKL_REPO="${opthash[-r]}"
(( ${+opthash[--repo]} )) && ZKL_REPO="${opthash[--repo]}"
(( ${+opthash[--req]} )) && ZKL_REQ="1"

noansi

if [ "$ZKL_HELP" = "1" ]; then
    pinfo "Usage: zkiresize [options]"
    print "Resizes given index (-i) to given size (-s)"
    print
    help-opts
    help-quiet
    help-nansi
    help-verb
    help-dry
    help-path
    help-repo
    help-dft
    help-index
    help-rsize
    help-list
    help-cons
    help-sect
    help-desc
    help-req

    exit 0
fi

quiet || dry-mode-msg

#
# Input data verification
#

if [[ "$ZKL_SIZE" -lt "0" ]]; then
    qerror "Negative index size given, aborting"
    exit 2
fi

if [[ "$ZKL_SIZE" -gt "150" ]]; then
    qerror "Maximum index size is 150, aborting"
    exit 3
fi

if [[ -n "$ZKL_SIZE" && "$ZKL_SIZE" != <-> ]]; then
    qerror "Given index size (-s/--size) is not a number (given: $ZKL_SIZE)"
    exit 14
fi

if [[ -n "$ZKL_INDEX" && "$ZKL_INDEX" != <-> ]]; then
    qerror "Given index (-i/--index) is not a number (given: $ZKL_INDEX)"
    exit 15
fi

if [[ "$ZKL_INDEX" -eq "0" ]]; then
    qerror "Index (-i/--index) cannot be 0"
    exit 16
fi

if [[ -n "$ZKL_SECTION" && "$ZKL_SECTION" != [A-Z] ]]; then
    qerror "Improper section given ($ZKL_SECTION), it must be single letter A-Z"
    exit 9
fi

if [[ "$ZKL_DESCRIPTION" != "[[]]" && "$ZKL_DESCRIPTION" != [[:space:][:alpha:]0-9_-]# ]]; then
    qerror "Improper description given ($ZKL_DESCRIPTION). Allowed are letters (unicode), digits, underscores, spaces and hyphens"
    exit 10
fi

#
# Read destination path
# Much of pure control code
#

if [ -n "$ZKL_REPO" ]; then
    local bkp_zkl_path="$ZKL_PATH"
    if resolve_repo; then
        ZKL_PATH="$REPLY"
    else
        qerror "Provided repository doesn't exist (path: $REPLY)"
        # Next try with path if user gave it, or when user gave
        # -d and ZEKYLL_DEFAULT_REPO is not empty
        if [[ -n "$bkp_zkl_path" ]]; then
            ZKL_PATH="$bkp_zkl_path"
            qerror "Trying with provided path: $ZKL_PATH"
        elif [[ "$ZKL_DEFAULT" = "1" && -n "$ZEKYLL_DEFAULT_REPO" ]]; then
            qerror "Trying with ZEKYLL_DEFAULT_REPO: $ZEKYLL_DEFAULT_REPO"
        else
            exit 13
        fi
    fi
fi

REPLY="$ZKL_PATH"
[ "$REPLY" = "" ] && resolve_path
ZKL_PATH="$REPLY"
cd_will_work_on "$REPLY" "$ZKL_VERBOSE" || exit 4

USE_GIT="1"
if ! test -d ".git"; then
    USE_GIT="0"
fi

#
# Work on the index
#

# Sets ZKL_INDEX_ZEKYLLS array. It is used by the index
# subsystem as reference of what files are potentially
# part of current index
set_index "$ZKL_INDEX"

if [ "$ZKL_LIST" != "1" ]; then

    # Focus on checking consistency
    if compute_index_size_check_consistency; then
        if [ "$ZKL_CONSISTENT" = "1" ]; then
            print "consistent"
            print -rl "${reply[@]}"
            exit 0
        else
            qinfo2 "Index of size $REPLY is consistent"
            qprint
        fi
    else
        if [ "$ZKL_CONSISTENT" = "1" ]; then
            print "inconsistent"
            print -rl "${reply2[@]}"
            exit 11
        else
            qerror "Inconsistent index (of size $REPLY)! The problematic files are:"
            qprintrl "${reply2[@]}"
            qerror "They are not in sequence with other files."
            exit 5
        fi
    fi

else

    # Focus on listing the files
    if compute_index_size_check_consistency -f; then
        print -rl -- "${reply[@]}"
        exit 0
    else
        print -rl -- "${reply[@]}"
        exit 12
    fi

fi


integer size="$REPLY"
local -a files
files=( "${reply[@]}" )

if [ "$ZKL_SIZE" = "" ]; then
    qinfo "You didn't request resize. Here are files of index $ZKL_INDEX, bye:"
    print -rl -- "${files[@]}"
    exit 6
fi

if [[ "$ZKL_SIZE" -eq "$size" ]]; then
    qprint "Current index size and requested index size are the same, exiting"
    exit 7
fi

if [[ "$ZKL_SIZE" -gt "$size" ]]; then
    qinfo "Requested index is greater, will create zekylls"

    # What zekylls need to be created?
    local -a new_zekylls
    integer lim="$ZKL_SIZE"
    new_zekylls=( "${(@)ZKL_INDEX_ZEKYLLS[size+1, lim]}" )
    if [ "$ZKL_PERFORM_WRITE" = "1" ]; then
        qinfon "Will create zekylls: "
    else
        qinfon "Would create zekylls (dry mode): "
    fi
    qprint ${new_zekylls[@]}
    qprint

    local write git quiet noansi
    [ "$ZKL_PERFORM_WRITE" = "1" ] && write="-w"
    [ "$USE_GIT" = "1" ] && git="-g"
    [ "$ZKL_QUIET" = "1" ] && quiet="-q"
    [ "$ZKL_NOANSI" = "1" ] && noansi="-q"

    local -a sec desc
    [ -n "$ZKL_SECTION" ] && sec=( "--section" "$ZKL_SECTION" )
    [ "$ZKL_DESCRIPTION" != "[[]]" ] && desc=( "--desc" "$ZKL_DESCRIPTION" )

    zkcreate -p "$ZKL_PATH" $write $git $quiet $noansi -z "${(j::)new_zekylls}" "${sec[@]}" "${desc[@]}"
else
    qinfo "Requested index is smaller, zekylls will be moved to \"_\"-beginning file names"

    # What zekylls need to be deleted?
    local -a kept_zekylls del_zekylls
    integer lim="$ZKL_SIZE"
    kept_zekylls=( "${(@)files[1,lim]}" )
    del_zekylls=( "${(@)files:|kept_zekylls}" )

    [[ "$ZKL_SIZE" -eq 0 ]] && perror "Will delete whole index"

    if [[ "$(( quiet ))" = "1" && "$ZKL_PERFORM_WRITE" = "1" ]]; then
        pinfo "Will delete following zekylls, are you sure? [y/n]"
        print
        print -rl -- "${del_zekylls[@]}"
        print

        local key
        read -qs key

        if [ "$key" = "n" ]; then
            perror "No agreement to continue, exiting"
            exit 8
        fi
    fi

    integer ts
    if type date 2>/dev/null 1>&2; then
        ts="$( date +%s )"
    else
        zmodload zsh/datetime
        ts="$EPOCHSECONDS"
    fi

    local f
    for f in "${del_zekylls[@]}"; do
        qprint "mv ${ZKLCOL[info2]}$f${ZKLCOL[rst]} _$f"
        if [ "$ZKL_PERFORM_WRITE" = "1" ]; then
            command mv "$f" "_${f}-$ts"
            [ "$USE_GIT" = "1" ] && git add "$f"
        fi
    done
fi
